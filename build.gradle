import javax.tools.JavaCompiler

plugins {
    id "com.diffplug.gradle.spotless" version "3.27.1"
}


allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'jacoco'

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
    }
}



def reportSubprojects() {
    return subprojects.findAll {
        (new File("${it.projectDir}/src/test/java").isDirectory())
    }
}

task jacocoMergeAll(type: JacocoMerge) {
    dependsOn(subprojects.test, subprojects.jacocoTestReport)
    reportSubprojects().each { subproject ->
        executionData subproject.tasks.withType(Test)
    }
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/generated/**')
        }))
    }
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(jacocoMergeAll)
    sourceDirectories.from = files(reportSubprojects().sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(reportSubprojects().sourceSets.main.output)
    executionData.from = files("${buildDir}/jacoco/jacocoMergeAll.exec")

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = true
    }
}
